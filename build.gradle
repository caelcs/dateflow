plugins {
    id("java-library")
    id("name.remal.sonarlint") version "1.5.0"
    id("io.freefair.lombok") version "6.3.0"
    id("maven-publish")
    id("pl.allegro.tech.build.axion-release") version "1.13.6"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

repositories {
    mavenCentral()
}

project.version = scmVersion.version

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.assertj:assertj-core:3.21.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.1.0'
    testImplementation 'org.junit-pioneer:junit-pioneer:1.5.0'
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
        events 'FAILED', 'SKIPPED', 'PASSED'
        showStandardStreams = true
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "\nTest result: ${result.resultType}"
                println "Test summary: ${result.testCount} tests, " +
                        "${result.successfulTestCount} succeeded, " +
                        "${result.failedTestCount} failed, " +
                        "${result.skippedTestCount} skipped"
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {

                name = 'DateFlow'
                description = 'Library to handle date time conversions'
                url = 'https://github.com/caelcs/dateflow'

                groupId = "com.dateflow"
                artifactId = "dateflow"
                version = version

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'caelcs'
                        name = 'Adolfo Custidiano'
                        email = 'info.cael@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:caelcs/dateflow.git'
                    developerConnection = 'scm:git:ssh:/caelcs/dateflow.git'
                    url = 'https://github.com/caelcs/dateflow'
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/caelcs/dateflow"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}


